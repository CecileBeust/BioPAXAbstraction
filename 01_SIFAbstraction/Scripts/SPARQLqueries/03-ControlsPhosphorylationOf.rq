PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX chebi: <http://purl.obolibrary.org/obo/chebi/>
PREFIX chebidb: <http://purl.obolibrary.org/obo/CHEBI_>
PREFIX chebirel: <http://purl.obolibrary.org/obo/CHEBI#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX bp3: <http://www.biopax.org/release/biopax-level3.owl#>
PREFIX reactome: <http://www.reactome.org/biopax/91/48887#>
PREFIX abstraction:<http://abstraction/#>
CONSTRUCT {
	?enzymeProt abstraction:controlsPhosphorylationOf ?protLeft
	}
WHERE {
  ?reaction rdf:type/(rdfs:subClassOf*) bp3:BiochemicalReaction .
  
  ?catalysis bp3:controlled ?reaction .
  ?catalysis rdf:type bp3:Catalysis .
  ?catalysis bp3:controller ?enzyme .
  ?enzyme ((bp3:component|bp3:memberPhysicalEntity)*) ?enzymeProt .
  ?enzymeProt rdf:type bp3:Protein .
  
  ?reaction bp3:left/((bp3:component|bp3:memberPhysicalEntity)*) ?protLeft .
  ?protLeft rdf:type bp3:Protein .
  ?protLeft bp3:entityReference ?protLeftRef .
  
  ?reaction bp3:right/((bp3:component|bp3:memberPhysicalEntity)*) ?protRight .
  ?protRight rdf:type bp3:Protein .
  ?protRight bp3:entityReference ?protRightRef .

  # Complete phosphorylation check
  ?protRight bp3:feature ?feature .
  ?feature rdf:type bp3:ModificationFeature .
  ?feature bp3:modificationType ?modificationType .
  ?modificationType rdf:type bp3:SequenceModificationVocabulary .
  ?modificationType bp3:term ?modificationTerm .
  FILTER(CONTAINS(LCASE(?modificationTerm), "phospho"))
 
  # Ensure left and right proteins are the same (unphosphorylated vs phosphorylated)
  FILTER(?protLeftRef = ?protRightRef)
}