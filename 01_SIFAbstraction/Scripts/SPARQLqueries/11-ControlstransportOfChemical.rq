PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX chebi: <http://purl.obolibrary.org/obo/chebi/>
PREFIX chebidb: <http://purl.obolibrary.org/obo/CHEBI_>
PREFIX chebirel: <http://purl.obolibrary.org/obo/CHEBI#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX bp3: <http://www.biopax.org/release/biopax-level3.owl#>
PREFIX reactome: <http://www.reactome.org/biopax/93/48887#>
PREFIX abstraction:<http://abstraction/#>

CONSTRUCT {
  ?enzymeProt abstraction:ControlsTransportOfChemical ?SmallMolecule1
}
WHERE {
  # Core reaction pattern
  ?SmallMolecule1 rdf:type bp3:SmallMolecule ;
        	bp3:cellularLocation ?cellularLocVocab1 .
  ?SmallMolecule2 rdf:type bp3:SmallMolecule ;
        	bp3:cellularLocation ?cellularLocVocab2 .
 
  ?SmallMolecule1 bp3:entityReference ?SmallMolecule1Ref .
  ?SmallMolecule2 bp3:entityReference ?SmallMolecule2Ref .
  ?cellularLocVocab1 bp3:term ?location1 .
  ?cellularLocVocab2 bp3:term ?location2 .

  ?reaction rdf:type bp3:BiochemicalReaction ;
                   	bp3:left ?SmallMolecule1 ;
                   	bp3:right ?SmallMolecule2 .

  ?catalysis bp3:controlled ?reaction .
  ?catalysis rdf:type bp3:Catalysis .
  ?catalysis bp3:controller ?enzyme .
  ?enzyme ((bp3:component|bp3:memberPhysicalEntity)*) ?enzymeProt .
  ?enzymeProt rdf:type bp3:Protein .
}