PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX chebi: <http://purl.obolibrary.org/obo/chebi/>
PREFIX chebidb: <http://purl.obolibrary.org/obo/CHEBI_>
PREFIX chebirel: <http://purl.obolibrary.org/obo/CHEBI#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX bp3: <http://www.biopax.org/release/biopax-level3.owl#>
PREFIX reactome: <http://www.reactome.org/biopax/93/48887#>
PREFIX abstraction:<http://abstraction/#>

CONSTRUCT {
  ?Prot1 abstraction:InteractsWith ?Prot2
}
WHERE {
  # Core molecular interaction pattern
  ?MolecularInteraction (rdf:type/rdfs:subClassOf*) bp3:MolecularInteraction .
  ?MolecularInteraction bp3:participant ?Participant1 .
  ?MolecularInteraction bp3:participant ?Participant2 .
  
  ?Participant1 ((bp3:component|bp3:memberPhysicalEntity)*) ?Prot1 .
  ?Prot1 rdf:type bp3:Protein .
  ?Participant2 ((bp3:component|bp3:memberPhysicalEntity)*) ?Prot2 .
  ?Prot2 rdf:type bp3:Protein .
  
  FILTER (STR(?Prot1) < STR(?Prot2))
}